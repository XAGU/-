apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.archinamon.aspectj'

// 加载 local.properties 资源
Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream() ;
properties.load( inputStream )

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.xiaolian.amigo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    signingConfigs {
        config {
            keyAlias 'android_amigo'
            keyPassword properties.getProperty('keyPassword')
            storeFile file('../android_amigo.jks')
            storePassword properties.getProperty('storePassword')
        }
    }

    buildTypes {
        debug {
//            buildConfigField("String", "BASE_URL", "\"http://www.xiaolian.io\"")
//            buildConfigField("String", "API_KEY", "\"111111\"")
            signingConfig signingConfigs.config
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 批量打包
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def fileName = "${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${releaseTime()}.apk"
                    output.outputFileName = fileName
                }
            }
        }
    }
    flavorDimensions "default"
    productFlavors {
        dev {
            // 包名
            applicationId defaultConfig.applicationId
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "笑联测试版"
            // 动态修改 常量 字段
            buildConfigField "String", "ENVIRONMENT", '"dev"'
            buildConfigField "String", "SERVER", "\"https://c.api.xiaolian365.com\""
            buildConfigField "String", "H5_SERVER", "\"https://c.h5.xiaolian365.com\""
            // 修改 AndroidManifest.xml 里渠道变量
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "dev"]
        }
        prod {
            applicationId defaultConfig.applicationId

            resValue "string", "app_name", "笑联"

            buildConfigField "String", "ENVIRONMENT", '"prod"'
            buildConfigField "String", "SERVER", "\"https://c.api.xiaolian365.com\""
            buildConfigField "String", "H5_SERVER", "\"https://c.h5.xiaolian365.com\""

            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "prod"]
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.daimajia.swipelayout:library:$rootProject.swipelayout"
    implementation "com.daimajia.easing:library:$rootProject.easing"
    implementation "com.daimajia.androidanimations:library:$rootProject.androidanimations"
    implementation "com.nineoldandroids:library:$rootProject.nineoldandroids"

    // recyclerview adapter
    implementation "com.zhy:base-rvadapter:$rootProject.baseRvadapter"
    implementation "com.scwang.smartrefresh:SmartRefreshLayout:1.0.3"
    compile "com.youth.banner:banner:1.4.10"

    // android support libraries
    implementation "com.android.support:palette-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$rootProject.supportLibraryVersion"

    // image
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    implementation "com.github.yalantis:ucrop:$rootProject.ucropVersion"
    implementation 'me.everything:overscroll-decor-android:1.0.4'
    implementation 'de.hdodenhof:circleimageview:2.1.0'

    // event bus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // gif
    implementation "pl.droidsonroids.gif:android-gif-drawable:1.2.3"

    // date picker
    implementation "cn.aigestudio.wheelpicker:WheelPicker:$rootProject.wheelpickerVersion"

    // rx permission
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'

    // 滑动返回
    implementation "com.aitangba:swipeback:$rootProject.swipebackVersion"

    // parser
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // network
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofit"

    // debug database
    // debugCompile "com.amitshekhar.android:debug-db:$rootProject.debugDBVersion"

    // dependency injection
    implementation "com.google.dagger:dagger:$rootProject.dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation 'javax.inject:javax.inject:1'

    // reactive
//    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjava2Version"
//    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"

    implementation "io.reactivex:rxjava:$rootProject.rxjava"
    implementation "io.reactivex:rxandroid:$rootProject.rxandroid"

    // code generator for view
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // swipe view
    implementation "com.mindorks:placeholderview:$rootProject.placeholderviewVersion"

    compileOnly "org.projectlombok:lombok:1.16.18"
    annotationProcessor "org.projectlombok:lombok:1.16.18"

    // dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"


    // UI Testing
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"

    // RxBLE
    implementation "com.polidea.rxandroidble:rxandroidble:$rootProject.polidea"
    implementation 'com.trello:rxlifecycle:1.0'
    implementation 'com.trello:rxlifecycle-components:1.0'

    // multi dex
    implementation 'com.android.support:multidex:1.0.2'

    // 友盟
    implementation "com.umeng.analytics:analytics:$rootProject.umeng"
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
}